#!/bin/tcsh
# This script will save you some time downloading data from fmri.umd.edu
# written by dmoracze on 9/21/2015
#
# Initialize variables and set defaults
set count 	= 1
set dir 	= `pwd`
set perm 	= 775
set opt 	= 1
set uname 	= $user
set subj 	= ()
# parse the inputs
if ($#argv == 0) then
	echo " "
	echo "**** No options given ****"
	echo " "
	echo "Here, this may help you:"
	set argv = (-h)
endif
while ($count <= $#argv)
	switch("$argv[$count]")
		case "-h":
			echo " "
			echo "Usage: dl.data [options] -s subjectname"
			echo " "
			echo "[options]:"
			echo "-h                  show this help file"
			echo "-s felix            felix will be the subject ID that you want to download"
			echo "                    to download more than one subject, use -s before every ID"
			echo "-dir ../study_dir   directory to download data to (absolute or relative path)"
			echo "                      default is current directory"
			echo "-study My Study     study ID directory from fmri server, quotes and escaped" 
			echo "                    spaces are essential"
			echo "                      default is RED Social Communication"
			echo "-PI Jane Doe        PI diretory from the fmri server, quotes and escaped"
			echo "                    spaces are essential"
			echo "                      default is Elizabeth Redcay"
			echo "-user dmitri        dmitri is the user who will log on to fmri (if not you,"
			echo "                    it will ask for password)"
			echo "                      default is current user"
			echo "-opt 1              choose from different options that will customize the -study"
			echo "                    and PI for you, for added convenience"
			echo "                    opt 1: PI = Elizabeth Redcay, study = RED"
			echo "                    opt 2: PI = Stefanie Kuchinsky, study = TVI"
			echo " "
		exit
		case "-s":
			@ count++
			set subj = ($subj $argv[$count])
		breaksw
		case "-dir":
			@ count++
			set dir = $argv[$count]
		breaksw
		case "-study":
			@ count++
			set study = "$argv[$count]"
		breaksw
		case "-user":
			@ count++
			set uname = $argv[$count]
		breaksw
		case "-PI":
			@ count++
			set PI = "$argv[$count]"
		breaksw
		case "-opt":
			@ count++
			set opt = $argv[$count]
		breaksw
	endsw
	shift
end
# check for subject IDs
if ($#subj == 0) then
	echo " "
	echo "No subject IDs given\!!"
	echo "dl.data -h for help file"
	echo " "
	exit
endif
# parse predefined options
if ($opt == 1) then
	set study = "RED\ Social\ Communication"
	set PI 	= "Elizabeth\ Redcay"
else if ($opt == 2) then
	set study = "TVI\ Timbre\ VS\ ID"
	set PI = "Stefanie\ Kuchinsky"
else
	echo " "
	echo "-opt not understood"
	echo "dl.data -h for help file"
	echo " "
	exit
endif
# download data and change permissions
foreach s ($subj)
	echo " "
	echo "PI:       $PI"
	echo "Study:    $study"
	echo "Subject:  $s"
	echo " "
	if (-d $dir/$s) then
		echo " "
		echo "Subject's folder already exists!"
		echo " "
		continue
	endif
	sleep 5
	scp -r ${uname}@fmri.umd.edu:"/export/software/fmri/massstorage/$PI/$study/$s/" $dir
	chgrp -R psyc-dscn-data $dir/$s
	chmod -R ug+w $dir/$s
	find $dir/$s -type d -exec chmod g+s {} \;
end
if ( ! -d $dir/$s ) then
	echo " "
	echo "============================================"
	echo "   Do or do not, there is no try. - Yoda    "
	echo "                                            "
	echo "   There was an error with your download    "
	echo "============================================"
	echo " "
else
	echo " "
	echo "============================================"
	echo "A long time ago on a server far, far away..."
	echo "============================================"
	echo " "
endif
exit